#+property: results silent

* Editor

#+BEGIN_SRC emacs-lisp
  (when (eq system-type 'windows-nt)
    (setenv "Home" (getenv "UserProfile")))

  (cond ((getenv "EMACS_HOME_DIR")
         (setq default-directory (getenv "EMACS_HOME_DIR")))
        ((eq system-type 'windows-nt)
         (setq default-directory (concat (getenv "UserProfile") "\\")))
        (t
         (setq default-directory "~/")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq locale-coding-system 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (set-selection-coding-system 'utf-8)
  (prefer-coding-system 'utf-8)

  (setq gc-cons-threshold (* 100 1024 1024)
        read-process-output-max (* 1024 1024))

  (setq visible-bell t
        fill-column 80
        make-backup-files nil
        query-replace-highlight t
        search-highlight t
        inhibit-splash-screen t
        indent-tabs-mode nil
        show-paren-delay 0)

  (delete-selection-mode)
  (add-hook 'text-mode-hook 'turn-on-auto-fill)
  (global-auto-revert-mode 1)
  (global-font-lock-mode t)
  (electric-pair-mode 1)
  (show-paren-mode 1)
  (global-undo-tree-mode)

  (add-hook 'prog-mode-hook #'hs-minor-mode)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun yes-or-no-p (arg)
    "An alias for y-or-n-p, because I hate having to type 'yes' or 'no'."
    (y-or-n-p arg))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq mouse-wheel-scroll-amount '(1))
  (setq mouse-wheel-progressive-speed nil)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (require 'saveplace)
  (save-place-mode 1)

  (when (getenv "EMACS_HOME_DIR")
    (setq save-place-file "/storage/.emacs-places")
    (run-with-idle-timer (* 30 60) t #'save-place-kill-emacs-hook))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (when (getenv "EMACS_HOME_DIR")
    (setq bookmark-default-file "/storage/.emacs-bookmarks"))
#+END_SRC

Will bounce between matching parens just like % in vi

#+BEGIN_SRC emacs-lisp
  (defun na-bounce-sexp ()
    (interactive)
    (let ((prev-char (char-to-string (preceding-char)))
          (next-char (char-to-string (following-char))))
      (cond ((string-match "[[{(<]" next-char) (forward-sexp 1))
            ((string-match "[\]})>]" prev-char) (backward-sexp 1))
            (t (error "%s" "Not on a paren, brace, or bracket")))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq password-cache t) ; enable password caching
  (setq password-cache-expiry (* 12 3600)) ; for twelfe hours (time in secs)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-zoom-in ()
    (interactive)
    (set-face-attribute 'default nil :height 
                        (+ (face-attribute 'default :height) 10)))

  (defun na-zoom-out ()
    (interactive)
    (set-face-attribute 'default nil :height 
                        (- (face-attribute 'default :height) 10)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (when  (not (eq system-type 'windows-nt))
    (autoload 'flyspell-mode "flyspell" "On-the-fly spelling checker." t)

    (add-hook 'message-mode-hook 'turn-on-flyspell)
    (add-hook 'text-mode-hook 'turn-on-flyspell)
    (add-to-list 'ispell-skip-region-alist '("+begin_src" . "+end_src"))

    (defun turn-on-flyspell ()
      "Force flyspell-mode on using a positive arg.  For use in hooks."
      (interactive)
      (flyspell-mode 1)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (require 'helm)

  (setq-default helm-M-x-fuzzy-match t)
  (global-set-key (kbd "M-x") #'helm-M-x)
#+END_SRC

* Languages
** Clojure

 #+BEGIN_SRC emacs-lisp
   (require 'clojure-mode)
   (require 'clojure-mode-extra-font-locking)
 #+END_SRC

** C/C++

 #+BEGIN_SRC emacs-lisp
   (add-to-list 'auto-mode-alist '("[.]pde$" . c++-mode))
   (add-to-list 'auto-mode-alist '("[.]ino$" . c++-mode))
   (add-to-list 'auto-mode-alist '("[.]h$" . c++-mode))
   (add-to-list 'auto-mode-alist '("[.]cpp$" . c++-mode))
 #+END_SRC

** Matlab

 #+BEGIN_SRC emacs-lisp
   (autoload 'matlab-mode "matlab" "Matlab Editing Mode" t)

   (add-to-list
    'auto-mode-alist
    '("\\.m$" . matlab-mode))

   (setq matlab-indent-function t)
   (setq matlab-shell-command "matlab")

   (eval-after-load 'matlab-mode 
     '(define-key matlab-mode-map (kbd "C-c C-c") 'matlab-shell-run-cell))
 #+END_SRC

** Python

 #+BEGIN_SRC emacs-lisp
   (require 'python)
   (setq python-shell-interpreter "python3")
   (setq python-indent-guess-indent-offset-verbose nil)
 #+END_SRC

** Docker

 #+BEGIN_SRC emacs-lisp
   (require 'dockerfile-mode)
   (require 'docker-compose-mode)

   (add-to-list 'auto-mode-alist '("Dockerfile\\'" . dockerfile-mode))
 #+END_SRC

** Latex

 #+BEGIN_SRC emacs-lisp
   (setq latex-run-command "pdflatex")

   (add-hook 'TeX-after-compilation-finished-functions
             #'TeX-revert-document-buffer)

   (defun tex-compile-without-changing-windows ()
     (interactive)
     (save-buffer)
     (save-window-excursion
       (tex-compile
	default-directory
	(concat latex-run-command " " buffer-file-name))))
 #+END_SRC

** Skeletons

 #+BEGIN_SRC emacs-lisp
   (setq abbrev-mode t)

   (add-hook 'emacs-lisp-mode-hook 'abbrev-mode)
   (add-hook 'clojure-mode-hook 'abbrev-mode)
   (add-hook 'c++-mode-hook 'abbrev-mode)
   (add-hook 'c-mode-hook 'abbrev-mode)
   (add-hook 'org-mode-hook 'abbrev-mode)

   (define-abbrev-table 'java-mode-abbrev-table '())
   (define-abbrev-table 'clojure-mode-abbrev-table '())
   (define-abbrev-table 'c++-mode-abbrev-table '())
   (define-abbrev-table 'org-mode-abbrev-table '())
 #+END_SRC

 org-mode

 #+BEGIN_SRC emacs-lisp
   (define-skeleton skel-org-babel-notebook-header
     ""
     nil
     "#+title: Notebook" \n
     "#+PROPERTY: header-args:jupyter-python :session /ssh:localhost:python :kernel python" \n
     "#+STARTUP: hidestars\n\n")
 #+END_SRC

 Clojure

 #+BEGIN_SRC emacs-lisp
   (define-skeleton skel-clojure-println
     ""
     nil
     "(println "_")")

   (define-abbrev clojure-mode-abbrev-table "prt" "" 'skel-clojure-println)

   (define-skeleton skel-clojure-defn
     ""
     nil
     "(defn "_" [])")

   (define-abbrev clojure-mode-abbrev-table "defn" "" 'skel-clojure-defn)

   (define-skeleton skel-clojure-if
     ""
     nil
     "(if ("_"))")

   (define-abbrev clojure-mode-abbrev-table "if" "" 'skel-clojure-if )

   (define-skeleton skel-clojure-let
     ""
     nil
     "(let ["_"] )")

   (define-abbrev clojure-mode-abbrev-table "let" "" 'skel-clojure-let)

   (define-skeleton skel-clojure-doseq
     ""
     nil
     "(doseq ["_"] "
     \n > ")")

   (define-abbrev clojure-mode-abbrev-table "doseq" "" 'skel-clojure-doseq)

   (define-skeleton skel-clojure-do
     ""
     nil
     "(do "_" "
     \n > ")")

   (define-abbrev clojure-mode-abbrev-table "do" "" 'skel-clojure-do)

   (define-skeleton skel-clojure-reduce
     ""
     nil
     "(reduce (fn[h v] ) "_" ) ")

   (define-abbrev clojure-mode-abbrev-table "reduce" "" 'skel-clojure-reduce)

   (define-skeleton skel-clojure-try
     ""
     nil
     "(try "_" (catch Exception e (println e)))")

   (define-abbrev clojure-mode-abbrev-table "try" "" 'skel-clojure-try)

   (define-skeleton skel-clojure-map
     ""
     nil
     "(map #() "_")")

   (define-abbrev clojure-mode-abbrev-table "map" "" 'skel-clojure-map)
 #+END_SRC

 C++

 #+BEGIN_SRC emacs-lisp
   (define-skeleton skel-cpp-prt
     ""
     nil
     \n >
     "std::cout << " _ " << std::endl;"
     \n >)

   (define-abbrev c++-mode-abbrev-table "cout"  "" 'skel-cpp-prt)

   (define-skeleton skel-cpp-fsm
     ""
     "Class Name: " \n >
     "class " str " {" \n >
     "void boot() { state = &" str "::shutdown; }" \n >
     "void shutdown() { }" \n >
     "void (" str "::* state)();" \n >
     "public:" \n >
     str "() : state(&" str "::boot) {}" \n >
     "void operator()() {(this->*state)();}" \n >
     "};"\n >)
 #+END_SRC

 Java

 #+BEGIN_SRC emacs-lisp
   (define-skeleton skel-java-println
     "Insert a Java println Statement"
     nil
     "System.out.println(" _ " );")

   (define-abbrev java-mode-abbrev-table "prt" "" 'skel-java-println )
 #+END_SRC
** Company & LSP

 #+BEGIN_SRC emacs-lisp
   (add-hook 'after-init-hook 'global-company-mode)
   (setq company-minimum-prefix-length 1)
   (global-set-key (kbd "TAB") #'company-indent-or-complete-common)
 #+END_SRC

 #+BEGIN_SRC emacs-lisp
   (require 'lsp-mode)
   (require 'lsp-java)

   (setq lsp-keymap-prefix "C-c l")
   (setq lsp-headerline-breadcrumb-enable nil)

   (dolist (dir '(
                  "[/\\\\]matlab_runtime"
                  ))
     (push dir lsp-file-watch-ignored))

   (add-hook 'java-mode-hook #'lsp-deferred)
   (add-hook 'clojure-mode-hook #'lsp-deferred)
   (add-hook 'c++-mode-hook #'lsp-deferred)
   (add-hook 'python-mode-hook #'lsp-deferred)

   (lsp-register-client
    (make-lsp-client :new-connection (lsp-stdio-connection '("terraform-ls" "serve"))
                     :major-modes '(terraform-mode)
                     :server-id 'terraform-ls))

   (add-hook 'terraform-mode-hook #'lsp-deferred)

   (lsp-register-client
    (make-lsp-client :new-connection (lsp-tramp-connection "pylsp")
                     :major-modes '(python-mode)
                     :remote? t
                     :server-id 'pyls-remote))
 #+END_SRC

Fix for - https://github.com/emacs-lsp/lsp-ui/issues/607

#+begin_src emacs-lisp :results silent
  (let ((areas '("mode-line" "left-margin" "left-fringe" "right-fringe" "header-line" "vertical-scroll-bar"))
        loc)
    (while areas
      (setq loc (pop areas))
      (global-set-key
       (kbd (concat "<" loc "> <mouse-movement>")) #'ignore)))
#+end_src

** Projectile

 #+BEGIN_SRC emacs-lisp
   (require 'projectile)
   (projectile-mode +1)
   (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
 #+END_SRC
  
* Org-Mode

#+BEGIN_SRC emacs-lisp
  (require 'ob)
  (require 'poly-org)
  (require 'org-superstar)

  (setq org-startup-folded t)
  (setq org-return-follows-link t)
  (setq org-startup-with-inline-images t)
  (setq org-image-actual-width nil)
  (setq org-src-window-setup 'current-window)
  (setq org-src-fontify-natively t)
  (setq org-confirm-babel-evaluate nil)
  (setq org-babel-python-command "python3")
  (setq org-hide-leading-stars t)

  (add-hook 'org-mode-hook (lambda () (org-superstar-mode 1)))
  (setq org-superstar-headline-bullets-list '("◉" "◉" "◉" "◉" "◉"))

  (when  (eq system-type 'windows-nt)
    (setq org-babel-python-command "python.exe"))

  (add-to-list 'auto-mode-alist '("\\.org$" . org-mode))
  (add-to-list 'auto-mode-alist '("\\.org" . poly-org-mode))

  (define-key poly-org-mode-map (kbd "C-c C-c") 'org-ctrl-c-ctrl-c)
  (pm-around-advice #'org-ctrl-c-ctrl-c #'polymode-with-current-base-buffer)

  ;; Run/highlight code using babel in org-mode
  (org-babel-do-load-languages
   'org-babel-load-languages
   '((clojure . t)
     (python . t)
     (shell . t)))

  (add-hook 'org-babel-after-execute-hook 'org-display-inline-images 'append)

  (setq org-structure-template-alist
        '(("el" . "src emacs-lisp\n")
          ("cl" . "src clojure\n")
          ("sh" . "src sh\n")
          ("jp" . "src jupyter-python\n")
          ("s" . "src")
          ("l" . "export latex")
          ("e" . "example")))

  (defun org-babel-kill-session ()
    "Kill session for current code block."
    (interactive)
    ;; (unless (org-in-src-block-p)
    ;;   (error "You must be in a src-block to run this command"))
    (save-window-excursion
      (org-babel-switch-to-session)
      (kill-buffer)))

  (defun org-babel-remove-result-buffer ()
    "Remove results from every code block in buffer."
    (interactive)
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward org-babel-src-block-regexp nil t)
        (org-babel-remove-result))))
#+END_SRC

#+begin_src emacs-lisp
  (setq org-refile-targets '((nil :maxlevel . 9)
			     (org-agenda-files :maxlevel . 9)))
  (setq org-outline-path-complete-in-steps nil)         ; Refile in a single go
  (setq org-refile-use-outline-path t)                  ; Show full paths for refiling
#+end_src

Agenda Management

#+BEGIN_SRC emacs-lisp
  (cond ((file-exists-p "~/org/")
         (setq na-agenda-folder "~/org/"))
        ((file-exists-p "/storage/org/")
         (setq na-agenda-folder "/storage/org/"))
        ((file-exists-p "~/source/org/")
         (setq na-agenda-folder "~/source/org/"))
        (t
         (setq na-agenda-folder "~/org/")))

  (setq na-agenda-files '("notes.org"
                          "inbox.org"
                          "bookmarks.org"
                          "shopping.org"
                          "devops.org"))

  (when (file-exists-p na-agenda-folder)
    (setq org-agenda-files
          (mapcar (lambda (f)
                    (concat na-agenda-folder f))
                  na-agenda-files)))

  (setq org-default-notes-file 
    (concat na-agenda-folder (car na-agenda-files)))

  (setq org-agenda-custom-commands
        '(("h" "Agenda and Todo"
           ((agenda "" ((org-agenda-span 7)
                        (org-agenda-start-on-weekday nil)))
            (tags-todo "personal/TODO")
            (tags-todo "work/TODO")
            (tags-todo "home/TODO")
            (tags-todo "personal/WAIT")
            (tags-todo "work/WAIT")
            (tags-todo "home/WAIT")))))

  (setq org-capture-templates
        '(("p" "Personal TODO" entry
           (file+headline (lambda () (concat na-agenda-folder "notes.org")) "Personal")
           "* TODO %?\n" :prepend t)
          ("r" "Robotics Lab TODO" entry
           (file+headline (lambda () (concat na-agenda-folder "notes.org")) "Robotics Lab")
           "* TODO %?\n" :prepend t)
          ("a" "Akademik TODO" entry
           (file+headline (lambda () (concat na-agenda-folder "notes.org")) "Akademik")
           "* TODO %?\n" :prepend t)
          ("b" "Read Later" entry
           (file+headline (lambda () (concat na-agenda-folder "bookmarks.org")) "Read Later")
           "* %?\n" :prepend t)))

  (setq org-agenda-window-setup 'current-window)
  (setq org-agenda-restore-windows-after-quit t)
  (setq org-agenda-show-all-dates t)
  (setq org-deadline-warning-days 150)
  (setq org-archive-subtree-save-file-p t)
  (org-toggle-sticky-agenda)

  (let ((window-configuration))
    (defun kill-org-agenda ()
      (interactive)
      (kill-this-buffer)
      (set-window-configuration window-configuration))
  
    (defun jump-to-org-agenda ()
      (interactive)
      (unless (get-buffer "*Org Agenda(h)*")
        (setq window-configuration (current-window-configuration))
        (delete-other-windows)
        (org-agenda nil "h")
        (org-agenda-redo)
        (local-set-key [f1] #'kill-org-agenda)
        (local-set-key "q" #'kill-org-agenda))))

  (global-set-key [f1] 'jump-to-org-agenda)

  (when (file-exists-p na-agenda-folder)
    (run-with-idle-timer (* 30 60) t #'jump-to-org-agenda))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq org-latex-prefer-user-labels t)

  (setq org-latex-pdf-process
        '("latexmk -pdflatex='lualatex -shell-escape -interaction nonstopmode' -pdf -f  %f"))
#+END_SRC

* Dired

#+BEGIN_SRC emacs-lisp
  (require 'dired)

  (setq large-file-warning-threshold nil)
  (setq ls-lisp-use-insert-directory-program nil)
  (setq ls-lisp-dirs-first t)
  
  (quelpa '(emacs-async
            :fetcher github :repo "jwiegley/emacs-async"))

  (autoload 'dired-async-mode "dired-async.el" nil t)
  (dired-async-mode 1)

  (setq dired-dwim-target t)
  (setq dired-recursive-deletes 'always)

  (add-hook 'dired-mode-hook
            (lambda ()
              (dired-hide-details-mode)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-dired-up-directory-after-kill ()
    "Call 'dired-up-directory' after calling '(kill-buffer (current-buffer))'."
    (interactive)
    (let* ((buf (current-buffer))
           (kill-curr (if (= (length (get-buffer-window-list buf)) 
                             1)
                          t nil)))
      (dired-up-directory)
      (when kill-curr
        (kill-buffer buf))))

  (defun na-dired-down-directory-after-kill ()
    "Call 'dired-find-alternate-file' after calling '(kill-buffer (current-buffer))'."
    (interactive)
    (let ((file (dired-get-filename))) 
      (if (file-directory-p file) 
          (let* ((buf (current-buffer))
                 (kill-curr (if (= (length (get-buffer-window-list buf)) 
                                   1)
                                t nil)))
            (dired-find-file)
            (when kill-curr
              (kill-buffer buf)))
        (dired-advertised-find-file))))

  (define-key dired-mode-map (kbd "C-w") 'na-dired-up-directory-after-kill)
  (define-key dired-mode-map (kbd "RET") 'na-dired-down-directory-after-kill)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq na-external-open-files-types 
        '("avi" "mp4" "flv" "wmv" "mov" "mkv" 
          "m4v" "mpg" "mpeg" "ts" "m3u"))

  (setq na-file-assocs (make-hash-table :test 'equal))

  (if (eq system-type 'gnu/linux)
      (progn 
        (puthash "avi" "vlc" na-file-assocs)
        (puthash "mp4" "vlc" na-file-assocs)
        (puthash "flv" "vlc" na-file-assocs)
        (puthash "wmv" "vlc" na-file-assocs)
        (puthash "mov" "vlc" na-file-assocs)
        (puthash "mkv" "vlc" na-file-assocs)
        (puthash "m4v" "vlc" na-file-assocs)
        (puthash "mpg" "vlc" na-file-assocs)
        (puthash "ts" "vlc" na-file-assocs)
        (puthash "mpeg" "vlc" na-file-assocs)))

  (if (eq system-type 'gnu/linux)
      (setq na-dired-external-viewer "xdg-open"))

  (defun na-dired-display-external (extension)
    "Open file at point in an external application."
    (interactive)
    (let ((file (dired-get-filename))
          (ext-viewer (gethash extension na-file-assocs))
          (process-connection-type nil))
      (if ext-viewer
          (start-process "" nil ext-viewer file)
        (start-process "" nil na-dired-external-viewer file))))

  (defun na-dired-open ()
    "Open file at point in an external application."
    (interactive)
    (let ((file-extension (file-name-extension 
                           (dired-get-filename))))
      (if file-extension
          (if (member (downcase file-extension) na-external-open-files-types)
              (na-dired-display-external (downcase file-extension))
            (na-dired-down-directory-after-kill))
        (na-dired-down-directory-after-kill))))

  (define-key dired-mode-map [return] 'na-dired-open)
#+END_SRC

* Tramp

#+BEGIN_SRC emacs-lisp
  (require 'tramp)

  (setq remote-file-name-inhibit-cache nil
        tramp-verbose 1
        tramp-completion-reread-directory-timeout nil)

  (setq tramp-default-method "ssh")

  (when (eq system-type 'windows-nt)
    (setq tramp-default-method "plink"))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-ssh-completions ()
    (mapcar
     (lambda (x)
       (car (cdr x)))
     (seq-filter
      (lambda (x)
        (car (cdr x)))
      (tramp-parse-sconfig "~/.ssh/config"))))

  (mapc (lambda (method)
          (tramp-set-completion-function 
           method '((tramp-parse-sconfig "~/.ssh/config"))))
        '("rsync" "scp" "sftp" "ssh"))
#+END_SRC

* Git

#+BEGIN_SRC emacs-lisp
  (require 'magit)

  (defalias 'mr 'magit-list-repositories)

  (setq git-committer-name "Nurullah Akkaya"
	git-committer-email "nurullah@nakkaya.com")

  (setq vc-follow-symlinks t
	magit-hide-diffs t
	magit-save-repository-buffers 'dontask)


  (remove-hook 'magit-section-highlight-hook 'magit-section-highlight)
  (remove-hook 'magit-section-highlight-hook 'magit-diff-highlight)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-magit-auto-commit-msg ()
    (concat
     "Update:\n"
     (string-join
      (mapcar
       (lambda (f)
         (concat "  " f "\n"))
       (magit-staged-files)))))

  (defun na-magit-auto-commit ()
    (interactive)
    (magit-call-git
     "commit" "-m" (na-magit-auto-commit-msg))
    (magit-refresh))

  (transient-append-suffix
    'magit-commit "a" '("u" "Auto Commit" na-magit-auto-commit))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq magit-repository-directories
        `(("~/org" . 0)
          ("~/source" . 1)
          ("~/Documents/GitHub/" . 1)
          ("/storage" . 1)))

  (setq magit-repolist-columns
        '(("Name"    25 magit-repolist-column-ident                  ())
          ("D"        1 magit-repolist-column-dirty                  ())
          ("L<U"      3 magit-repolist-column-unpulled-from-upstream ((:right-align t)))
          ("L>U"      3 magit-repolist-column-unpushed-to-upstream   ((:right-align t)))
          ("Path"    99 magit-repolist-column-path                   ())))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-magit-fetch-all-repositories ()
    "Run `magit-fetch-all' in all repositories returned by `magit-list-repos`."
    (interactive)
    (dolist (repo (magit-list-repos))
      (let ((default-directory repo))
        (magit-call-git "fetch" "--all")))
    (revert-buffer))

  (defun na-magit-push-all-repositories ()
    "Run `magit-push' in all repositories returned by `magit-list-repos`."
    (interactive)
    (dolist (repo (magit-list-repos))
      (let ((default-directory repo))
        (let ((current-branch (magit-get-current-branch)))
          (magit-call-git "push" "origin" current-branch))))
    (revert-buffer))

  (defun na-magit-auto-commit-multi-repo (&optional _button)
    "Show the status for the repository at point."
    (interactive)
    (--if-let (tabulated-list-get-id)
        (let* ((file (expand-file-name it))
               (default-directory (file-name-directory file)))
          (magit-call-git "add" "-A")
          (magit-call-git "commit" "-m" (na-magit-auto-commit-msg)))
      (user-error "There is no repository at point"))
    (revert-buffer))

  (add-hook 'magit-repolist-mode-hook
            (lambda ()
              (define-key magit-repolist-mode-map (kbd "f") #'na-magit-fetch-all-repositories)
              (define-key magit-repolist-mode-map (kbd "p") #'na-magit-push-all-repositories)
              (define-key magit-repolist-mode-map (kbd "c") #'na-magit-auto-commit-multi-repo)))
#+END_SRC

* Terminal

#+BEGIN_SRC emacs-lisp
  (if (eq system-type 'windows-nt)
      (progn
        (setenv "PATH"
                (concat
                 "C:\\Program Files\\CMake\\bin;"
                 "C:\\MinGW\\bin;"
                 "$HOME\\.rclone/;"
                 "$HOME\\Documents\\;"
                 "$HOME\\AppData\\Roaming\\Python\\Python36\\Scripts/;"
                 "$HOME\\AppData\\Roaming\\Python\\Python39\\Scripts/;"
                 "C:\\Program Files\\Arduino;"
                 (getenv "PATH")))
        (setenv "C_INCLUDE_PATH" "C:\\MinGW\\include")
        (setenv "CPLUS_INCLUDE_PATH" "C:\\MinGW\\include"))
    (setenv "PATH"
            (concat
             "/usr/local/bin:"
             (concat (getenv "HOME") "/.bin:")
             (concat (getenv "HOME") "/.local/bin:")
             (concat (getenv "HOME") "/.git-annex.linux:")
             (concat (getenv "HOME") "/.rclone:")
             (getenv "PATH"))))

  (when (eq system-type 'darwin)
    (setq exec-path (split-string (getenv "PATH") ":")))

  (setenv "PAGER" "cat")
  ;; (setenv "DISPLAY" ":0")
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (require 'eshell)
  (require 'em-alias)
  (require 'em-tramp) ; to load eshell’s sudo

  (setq eshell-hist-ignoredups t
        eshell-ls-initial-args '("-a")              ; list of args to pass to ls (default = nil)
        eshell-prefer-lisp-functions t              ; prefer built-in eshell commands to external ones
        eshell-visual-commands '("htop" "ssh" "nvtop")
        eshell-prompt-regexp (rx bol "\u03BB" space)
        eshell-banner-message ""
        eshell-cmpl-cycle-completions nil
        eshell-scroll-to-bottom-on-input 'all
        eshell-destroy-buffer-when-process-dies t)

  (add-hook 'eshell-mode-hook
            (lambda ()
              (define-key eshell-mode-map (kbd "<up>") #'eshell-previous-input)
              (define-key eshell-mode-map (kbd "<down>") #'eshell-next-input)))

  (defun eshell-mode-setup-function () 
    (company-mode -1))

  (add-hook 'eshell-mode-hook 'eshell-mode-setup-function)

  (eshell/alias "df" "df -h")
  (eshell/alias "ps-grep" "ps ax | grep -i $1")
  (eshell/alias "sudo" "eshell/sudo $*")
  (eshell/alias "docker" "/usr/bin/docker $*")

  ;; nq exec remote file
  (defun eshell/rnq (host file &rest options)
    (let ((default-directory (concat "/ssh:" host ":~")))
      (eshell/echo
       (shell-command-to-string
	(concat "nq " file " "
		(string-join
		 (mapcar 'prin1-to-string options) " "))))))

  (defun eshell/rkill (host pid)
    (let ((default-directory (concat "/ssh:" host ":~")))
      (eshell/echo
       (shell-command-to-string (concat "kill -9 " (number-to-string pid))))))

  ;; mirror host /folder/
  (eshell/alias "mirror" "rsync -avuz -e ssh $2 $1:$2 --delete")
  (eshell/alias "rcp" "rsync -rvLK $1 $2 --delete")

  ;;Clear the eshell buffer.
  (defun eshell/clear ()
    (let ((eshell-buffer-maximum-lines 0))
      (eshell-truncate-buffer)))

  (defalias 'cls 'eshell/clear)

  (defun eshell/gst (&rest args)
    (magit-status)
    (eshell/echo))

  (eshell/alias "ggc" "git repack -ad; git gc")
  (eshell/alias "gd" "magit-diff-unstaged")
  (eshell/alias "ga" "git annex  $*")
  (eshell/alias "gas" "git annex sync")
  (eshell/alias "gag" "git annex get . --not --in here")
  (eshell/alias "gac" "git annex add . && git annex sync --content")

  (defun eshell/pshell ()
    (insert
     (concat "powershell.exe -windowstyle hidden -Command"
             " \"Start-Process powershell  -ArgumentList '-NoExit',"
             " '-Command cd " default-directory "' -Verb runAs\""))
    (eshell-send-input))

  (eshell/alias "rclone-mount" "mkdir $2 && rclone mount $1:$2/ $2/ &")
  (eshell/alias "rclone-umount" "fusermount -u $1 && rm -rf $1")
  (eshell/alias "rclone-sync" "rclone -v sync $2/ $1:$2/")
  (eshell/alias "rclone-pull" "rclone copy -u -v $1:$2/ $2/")
  (eshell/alias
   "rclone-two-way"
   (concat "rclone copy -u -v $1:$2/ $2/" "&&" "rclone -v sync $2/ $1:$2/"))

  (defun pcomplete/conn ()
    (pcomplete-here* (na-ssh-completions)))

  (defun pcomplete/tmux-ssh ()
    (pcomplete-here* (na-ssh-completions)))

  (eshell/alias "conn" "cd /ssh:$1:~")
  (eshell/alias "tmux-ssh" "ssh $1 -t \"tmux attach\"")
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-shell-git-branch (pwd)
    (interactive)
    (if (and (eshell-search-path "git")
             (locate-dominating-file pwd ".git"))
        (concat " \u2014 " (magit-get-current-branch))
      ""))

  (setq eshell-prompt-function
        (lambda ()
          (concat
           (propertize (format-time-string "%H:%M" (current-time)) 'face `(:foreground "Grey50"))
           (propertize " \u2014 " 'face `(:foreground "Grey30"))
           (propertize (eshell/pwd) 'face `(:foreground "Grey50"))
           (propertize (na-shell-git-branch (eshell/pwd)) 'face `(:foreground "Grey50"))
           (propertize "\n" 'face `(:foreground "Grey30"))
           (propertize (if (= (user-uid) 0) "# " "\u03BB ") 'face `(:foreground "DeepSkyBlue3")))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (require 'multi-term)
  (setq multi-term-program "/bin/bash")
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defun na-new-term(term-or-shell)
    "Open a new instance of eshell."
    (interactive "P")
    (if term-or-shell
        (progn
          (multi-term)
          ;;pass C-c
          (define-key term-raw-map [?\C-c] 'term-send-raw))
      (eshell 'N)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (defconst na-sources-dir
    (if (eq system-type 'windows-nt)
        (expand-file-name "~/Documents/GitHub/")
      (expand-file-name "~/source")))

  (defun na-list-directories (f)
    (seq-filter
     (lambda (x)
       (file-directory-p
        (expand-file-name (concat f "/" x))))
     (directory-files f nil "^\\([^.]\\|\\.[^.]\\|\\.\\..\\)")))

  (defun pcomplete/src ()
    "Completion for `src'"
    (pcomplete-here* (na-list-directories na-sources-dir)))

  (defun src (&optional d)
    (let ((dir (if d
                   (concat na-sources-dir "/" d)
                 na-sources-dir)))
      (eshell/cd (expand-file-name dir))))

  (defun pcomplete/usb ()
    "Completion for `usb'"
    (pcomplete-here* (na-list-directories "/media/nakkaya")))

  (defun usb (d)
    (eshell/cd
     (expand-file-name
      (concat "/media/nakkaya/" d))))

  (defun pcomplete/cdb ()
    "Completion for `cdb'"
    (pcomplete-here* (mapcar (function buffer-name) (buffer-list))))

  (defun cdb (b)
    (eshell/cd
     (expand-file-name
      (with-current-buffer (get-buffer b)
        default-directory))))
#+END_SRC

Fix mouse when using emacsclient in tty and daemon launches via gui.

#+BEGIN_SRC emacs-lisp
  (defun my-terminal-config (&optional frame)
    "Establish settings for the current terminal."
    (if (not frame) ;; The initial call.
        (xterm-mouse-mode 1)
      ;; Otherwise called via after-make-frame-functions.
      (if xterm-mouse-mode
          ;; Re-initialise the mode in case of a new terminal.
          (xterm-mouse-mode 1))))

  ;; Evaluate both now (for non-daemon emacs) and upon frame creation
  ;; (for new terminals via emacsclient).
  (my-terminal-config)
  (add-hook 'after-make-frame-functions 'my-terminal-config)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq async-shell-command-display-buffer t
	async-shell-command-buffer 'new-buffer)

  (add-to-list 'display-buffer-alist
	       (cons "\\*Async Shell Command\\*.*" (cons #'display-buffer-no-window nil)))
#+END_SRC

* IBuffer

#+BEGIN_SRC emacs-lisp
  (setq ibuffer-saved-filter-groups
        (quote (("default"
                 ("Notes"
                  (or 
                   (name . "^passwd.org$")
                   (name . "^notes.org$")
                   (name . "^notes.org_archive$")
                   (name . "^bookmarks.org$")
                   (name . "^bookmarks.org_archive$")
                   (name . "^inbox.org$")
                   (name . "^inbox.org_archive$")
                   (name . "^devops.org$")
                   (name . "^devops.org_archive$")
                   (name . "^shopping.org$")
                   (name . "^shopping.org_archive$")))
                 ("Documents" (mode . pdf-view-mode))
                 ("Source" (or
                            (mode . java-mode)
                            (mode . clojure-mode)
                            (mode . org-mode)
                            (mode . bibtex-mode)
                            (mode . latex-mode)
                            (mode . xml-mode)
                            (mode . nxml-mode)
                            (mode . scheme-mode)
                            (mode . python-mode)
                            (mode . ruby-mode)
                            (mode . shell-script-mode)
                            (mode . sh-mode)
                            (mode . c-mode)
                            (mode . lisp-mode)
                            (mode . cperl-mode)
                            (mode . pixie-mode)
                            (mode . yaml-mode)
                            (mode . asm-mode)
                            (mode . emacs-lisp-mode)
                            (mode . c++-mode)
                            (mode . makefile-bsdmake-mode)
                            (mode . makefile-mode)
                            (mode . makefile-gmake-mode)
                            (mode . matlab-mode)
                            (mode . css-mode)
                            (mode . js-mode)
                            (mode . terraform-mode)
                            (mode . dockerfile-mode)
                            (mode . docker-compose-mode)
                            (name . "^\\*jupyter-.*")))
                 ("Terminal" (or (mode . term-mode)
                                 (mode . inferior-lisp-mode)
                                 (mode . inferior-python-mode)
                                 (name . "^*MATLAB.*")
                                 (name . "^*monroe.*")
                                 (name . "^*eshell.*")
                                 (name . "^\\*offlineimap\\*$")
				 (name . "^*Async Shell.*")))
                 ("Dired" (or (mode . dired-mode) 
                              (mode . sr-mode)))
                 ("Magit" (or (name . "^\\*magit.*\\*$")
                              (mode . magit-status-mode)
                              (mode . magit-diff-mode)
                              (mode . magit-process-mode)
                              (mode . magit-stash-mode)
                              (mode . magit-revision-mode)
                              (mode . magit-log-mode)))
                 ("Emacs" (or
                           (name . "^\\*Process List\\*$")
                           (name . "^\\*Dired log\\*$")
                           (name . "^\\*info\\*$")
                           (name . "^\\*Man.*\\*$")
                           (name . "^\\*tramp.+\\*$")
                           (name . "^\\*trace.+SMTP.+\\*$")
                           (name . "^\\.todo-do")
                           (name . "^\\*scratch\\*$")
                           (name . "^\\*git-status\\*$")
                           (name . "^\\*git-diff\\*$")
                           (name . "^\\*git-commit\\*$")
                           (name . "^\\*Git Command Output\\*$")
                           (name . "^\\*Org Export/Publishing Help\\*$")
                           (name . "^\\*Org-Babel Error Output\\*$")
                           (name . "^\\*Org PDF LaTeX Output\\*$")
                           (name . "^\\*Org Agenda\\*$")
                           (name . "^\\*Calendar\\*$")
                           (name . "^\\*Messages\\*$")
                           (name . "^\\*Completions\\*$")
                           (name . "^\\*Warnings\\*$")
                           (name . "^\\*Org Agenda.*\\*$")
                           (name . "^\\*Org Help\\*$")
                           (name . "^\\*Backtrace\\*$")
                           (name . "^TAGS$")
                           (name . "^\\*Help\\*$")
                           (name . "^\\*Shell Command Output\\*$")
                           (name . "^\\*Calculator\\*$")
                           (name . "^\\*Calc Trail\\*$")
                           (name . "^\\*Compile-Log\\*$")
                           (name . "^\\*quelpa-build-checkout\\*$")
                           (name . "^\\*helm M-x\\*$")
                           (name . "^\\*transmission\\*$")
                           (name . "^\\*lsp-.*")
                           (name . "^\\*jdtls.*")
                           (name . "^\\*clangd.*")
                           (name . "^\\*Flymake.*")
                           (name . "^\\*Native-.*")
                           (name . "^\\*Async-native-.*")
                           (name . "^\\*emacs\\*$")
                           (name . "^\\*GNU Emacs\\*$")
                           (name . "^\\*compilation\\*$")
                           (name . "^\\*elfeed-.*")
                           (name . "^\\*pylsp.*")
			   (name . "^\\*pyls-remote*")
			   (name . "^\\*LSP Error List*")))))))

  (setq ibuffer-show-empty-filter-groups nil)

  (add-hook 'ibuffer-mode-hook
            (lambda ()
              (ibuffer-switch-to-saved-filter-groups "default")))

  (setq ibuffer-expert t)

  (setq ibuffer-formats '((mark modified read-only " "
                                (name 18 18 :left :elide)
                                " "
                                (mode 16 16 :left :elide)
                                " " filename-and-process)
                          (mark " "
                                (name 16 -1)
                                " " filename)))
#+END_SRC

* Popper

#+begin_src emacs-lisp
  (use-package popper
    :ensure t ; or :straight t
    :bind (("M-q"   . popper-toggle-latest)
           ("M-\\"   . popper-cycle)
           ("C-M-\\" . popper-toggle-type))
    :init
    (setq popper-reference-buffers
          '("^*eshell.*"
	    "^*monroe.*"
	    "^*MATLAB.*"
	    term-mode
	    "^*jupyter-repl.*"
	    inferior-python-mode
	    inferior-lisp-mode
            compilation-mode))
    (popper-mode +1)
    (popper-echo-mode +1))
#+end_src

* Key Bindings

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-]")  'ibuffer)
  (global-set-key (kbd "C-c c") 'org-capture)
  (global-set-key (kbd "C-\\") 'other-window)
  (global-set-key (kbd "C-d")  'na-bounce-sexp)
  
  (global-set-key (kbd "C-x t") 'na-new-term)
  (add-hook 'term-mode-hook
            '(lambda ()
	       (define-key term-raw-map [(control \])] 'ibuffer)
               (define-key term-raw-map (kbd "C-y")  'term-paste)
               (define-key term-raw-map (kbd "C-\\") 'other-window)
	       (define-key term-raw-map (kbd "M-\\") 'popper-cycle)
	       (define-key term-raw-map (kbd "M-q") 'popper-toggle-latest)))

  (global-set-key "\M-[1;5C" 'forward-word)
  (global-set-key "\M-[1;5D" 'backward-word)
  (global-set-key "\M-[1;5A" 'backward-paragraph)
  (global-set-key "\M-[1;5B" 'forward-paragraph)

  (global-set-key (kbd "C-S-<left>") 'shrink-window-horizontally)
  (global-set-key (kbd "C-S-<right>") 'enlarge-window-horizontally)
  (global-set-key (kbd "C-S-<down>") 'shrink-window)
  (global-set-key (kbd "C-S-<up>") 'enlarge-window)

  (global-set-key (kbd "C-c <right>") 'hs-show-block)
  (global-set-key (kbd "C-c <left>")  'hs-hide-block)
  (global-set-key (kbd "C-c <up>")    'hs-hide-all)
  (global-set-key (kbd "C-c <down>")  'hs-show-all)
#+END_SRC

* Theme

#+BEGIN_SRC emacs-lisp
  (setq frame-title-format (list "Emacs " emacs-version))

  (when window-system
    (let ((font-dejavu "DejaVu Sans Mono 11")
          (font-monaco "Monaco 10")
          (font-jetbrains "Jet Brains Mono 14")
          (font-jetbrains-w32 "JetBrains Mono-11"))

      (cond ((x-list-fonts font-jetbrains)
             (set-frame-font font-jetbrains nil t))
            ((x-list-fonts font-monaco)
             (set-frame-font font-monaco nil t))
            ((x-list-fonts font-dejavu)
             (set-frame-font font-dejavu nil t))
            ((x-list-fonts font-jetbrains-w32)
             (set-frame-font font-jetbrains-w32 nil t)))))

  (when (eq system-type 'darwin)
    (set-face-attribute 'default nil :height 140))

  (unless (member "all-the-icons" (font-family-list))
    (all-the-icons-install-fonts t))

  (load-theme 'doom-one t)

  (set-face-background 'org-block "unspecified")
  (set-face-background 'org-block-begin-line "unspecified")
  (set-face-background 'org-block-end-line "unspecified")
  (setq poly-lock-allow-background-adjustment nil)

  (set-face-background 'mode-line          "SteelBlue4")
  (set-face-background 'mode-line-inactive "SlateGray4")
  (set-face-foreground 'mode-line          "gray5")
  (set-face-foreground 'mode-line-inactive "gray15")

  (require 'doom-modeline)
  (add-hook 'after-init-hook #'doom-modeline-mode)
  (setq doom-modeline-major-mode-icon nil)
  (setq doom-modeline-buffer-state-icon nil)
  (setq doom-modeline-buffer-encoding nil)

  (eval-after-load "magit"
    '(progn
       (remove-hook 'magit-section-highlight-hook 'magit-diff-highlight)
       (remove-hook 'magit-section-highlight-hook 'magit-section-highlight)
       (set-face-background 'magit-diff-context "unspecified")
       (set-face-background 'magit-diff-added-highlight "unspecified")
       (set-face-background 'magit-diff-context-highlight "unspecified")
       (set-face-background 'magit-diff-added "unspecified")
       (set-face-background 'magit-diff-removed "unspecified")
       (set-face-background 'magit-diff-hunk-heading "unspecified")
       (set-face-background 'magit-diff-removed-highlight "unspecified")))

  (set-face-background 'show-paren-match (face-background 'default))
  (set-face-attribute 'show-paren-match nil :foreground "red")
  (set-face-background 'show-paren-match "unspecified")

  (column-number-mode 1)
  (blink-cursor-mode 1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (toggle-scroll-bar -1)
  (tool-bar-mode -1)

  (defun na-reset-window-size ()
    (interactive)
    (when window-system
      (set-frame-size (selected-frame) 80 25)))

  (add-hook 'window-setup-hook 'na-reset-window-size)

  (defun fix-theme-in-tty (&optional frame)
    (or frame (setq frame (selected-frame)))
    "unsets the background color in terminal mode"
    (unless (display-graphic-p frame)
      (set-face-background 'default "unspecified-bg" frame)))

  (add-hook 'after-make-frame-functions 'fix-theme-in-tty)
  (add-hook 'window-setup-hook 'fix-theme-in-tty)
#+END_SRC

* Server

#+begin_src emacs-lisp
  (when (getenv "EMACS_HOME_DIR")
    (setq server-socket-dir "/opt/emacsd/server")
    (setq server-name "emacsd")
    (defun server-ensure-safe-dir (dir) "Noop" t))

  (unless (server-running-p)
    (server-start))
#+end_src
